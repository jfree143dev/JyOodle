/*
Copyright (c) 2011, Joseph Freeman <jfree143dev AT gmail DOT com>
All rights reserved.

Redistribution and use in source and binary forms, with or without modification,
are permitted provided that the following conditions are met:

 * Redistributions of source code must retain the above copyright notice,
   this list of conditions and the following disclaimer.
 * Redistributions in binary form must reproduce the above copyright notice,
   this list of conditions and the following disclaimer in the documentation
   and/or other materials provided with the distribution.
 * Neither the name of Joseph Freeman nor the names of its contributors may
   be used to endorse or promote products derived from this software without
   specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT,
INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED
OF THE POSSIBILITY OF SUCH DAMAGE.
*/

Package cps450.oodle;

Helpers
  letter = ['A'..'Z'] | ['a'..'z'];
  digit =  ['0'..'9'];

  cr =        13;
  lf =        10;
  not_cr_lf = [[0..127] - [cr + lf]];

  tab =   9;
  space = ' ';
  ws =    (tab | space);

  esc_seq_char = [[[[['t' + 'n'] + 'f'] + 'r'] + '"'] + '\'];
  esc_seq_octal= ['0'..'7']['0'..'7']['0'..'7'];
  esc_seq =      '\'(esc_seq_char | esc_seq_octal);

Tokens
  kw_and =      'and';
  kw_boolean =  'boolean';
  kw_begin =    'begin';
  kw_class =    'class';
  kw_else =     'else';
  kw_end =      'end';
  kw_false =    'false';
  kw_from =     'from';
  kw_if =       'if';
  kw_inherits = 'inherits';
  kw_int =      'int';
  kw_is =       'is';
  kw_loop =     'loop';
  kw_me =       'me';
  kw_new =      'new';
  kw_not =      'not';
  kw_null =     'null';
  kw_or =       'or';
  kw_string =   'string';
  kw_then =     'then';
  kw_true =     'true';
  kw_while =    'while';
  
  op_assign = ':=';
  
  op_lt_eq = '<=';
  op_gt_eq = '>=';
  op_lt =    '<';
  op_gt =    '>';
  op_eq =    '=';
  
  op_concat = '&';

  op_plus =  '+';
  op_minus = '-';
  op_mult =  '*';
  op_div =   '/';
  
  misc_comma =     ',';
  misc_colon =     ':';
  misc_semicolon = ';';
  misc_period =    '.';

  misc_l_paren = '(';
  misc_r_paren = ')';
  
  misc_l_brack = '[';
  misc_r_brack = ']';

  id = ('_' | letter) ('_' | letter | digit)*;

  int_lit = '-'? digit+;
  
  str_lit =              '"' (esc_seq | [[not_cr_lf - '"'] - '\'])*  '"';
  str_lit_unterminated = '"' (esc_seq | [[not_cr_lf - '"'] - '\'])*;
  str_lit_illegal =      '"' ([not_cr_lf - '"'])*  '"';

  eol = cr | lf | cr lf;
  
  line_cont = ws+ '_' (cr | lf | cr lf);

  blank = ws+;

  comment = '~' not_cr_lf*;
  
  illegal_char = [0..127];

Ignored Tokens
  line_cont, blank, comment;
  
Productions
	start                             {-> start}
	    = [cr1]:cr_star klass_list    {->New start([klass_list.klass])}
	    ;
  
	cr_star = T.eol*;
  
	cr_plus = T.eol+;
	
	klass_list                                              {-> klass*}
	    = {single} [head]:klass cr_star                     {-> [head.klass]}
	    | {multi} [head]:klass cr_plus [tail]:klass_list    {-> [head.klass, tail.klass]}
	    ;

	klass                 {-> klass}
	    = klass_header
	      klass_body
          klass_footer    {-> New klass(klass_header.klass_header, klass_body.klass_body, klass_footer.klass_footer)}
         ;

	klass_header                                             {-> klass_header}
	    = T.kw_class T.id klass_inherits? T.kw_is cr_plus    {-> New klass_header(T.id, klass_inherits)}
	    ;
	klass_inherits                        {-> klass_inherits}
	    = T.kw_inherits T.kw_from T.id    {-> New klass_inherits(T.id)}
	    ;
	klass_body                  {-> klass_body}
	    = klass_var* method*    {-> New klass_body([klass_var.var], [method])}
	    ;
	klass_footer           {-> klass_footer}
	    = T.kw_end T.id    {-> New klass_footer(T.id)}
	    ;
	
	klass_var    {-> var}
	    = var    {-> var.var}
	    ;

	var                                         {-> var}
	    = T.id var_type? var_assign? cr_plus    {-> New var(T.id, var_type.type, var_assign.expr)}
	    ;
  
	var_type                   {-> type}
	    = T.misc_colon type    {-> type.type}
	    ;

	var_assign                {-> expr}
	    = T.op_assign expr    {-> expr.expr}
	    ;
	
	method_var   {-> var}
	    = var    {-> var.var}
	    ;
	
	method_sig                                                      {-> method_sig}
	    = T.id T.misc_l_paren arg_list? T.misc_r_paren var_type?    {-> New method_sig(T.id, [arg_list.arg], var_type.type)}
	    ;
  
	method                                 {-> method}
	    = method_sig T.kw_is [cr1]:cr_plus
          method_var*
          T.kw_begin [cr2]:cr_plus
          stmt_list
          T.kw_end T.id [cr3]:cr_plus      {-> New method(method_sig, [method_var.var], [stmt_list.stmt])}
         ;

	arg_list                    {-> arg*}
	    = arg arg_list_tail*    {-> [arg, arg_list_tail.arg]}
	    ;
	arg_list_tail                 {-> arg}
	    = T.misc_semicolon arg    {-> arg}
	    ;
  
	arg                             {-> arg}
	    = T.id T.misc_colon type    {-> New arg(T.id, type)}
	    ;

	stmt_list            {-> stmt*}
	    =  stmt_line*    {-> [stmt_line.stmt]}
	    ;

	stmt_line             {-> stmt}
	    = stmt cr_plus    {-> stmt}
	    ;

	stmt                                                   {-> stmt}
	    = {assign} T.id arr_expr_list? T.op_assign expr    {-> New stmt.assign(T.id, [arr_expr_list.expr], T.op_assign, expr)}
        | {if} [if1]:T.kw_if expr T.kw_then cr_plus
               stmt_list
               stmt_else?
               T.kw_end [if2]:T.kw_if                      {-> New stmt.if(expr, T.kw_then, [stmt_list.stmt], [stmt_else.stmt])}
        | {loop} [lp1]:T.kw_loop T.kw_while expr cr_plus
                 stmt_list
                 T.kw_end [lp2]:T.kw_loop                  {-> New stmt.loop(T.kw_while, expr, [stmt_list.stmt])}
        | {call} call                                      {-> New stmt.call(call.call)}
        ;

	stmt_else                            {-> stmt*}
	    = T.kw_else cr_plus stmt_list    {-> [stmt_list.stmt]}
	    ;

	type                                                      {-> type}
	    = {int} T.kw_int                                      {-> New type.int(T.kw_int)}
        | {string} T.kw_string                                {-> New type.string(T.kw_string)}
        | {bool} T.kw_boolean                                 {-> New type.bool(T.kw_boolean)}
        | {udt} T.id                                          {-> New type.udt(T.id)}
        | {array} type T.misc_l_brack expr? T.misc_r_brack    {-> New type.array(type, T.misc_l_brack, expr, T.misc_r_brack)}
        ;

	expr                                           {-> expr}
	    = {or}    [e1]:expr1 T.kw_or [e2]:expr1    {-> New expr.or(e1.expr, T.kw_or, e2.expr)}
	    | {other} expr1                            {-> expr1.expr}
	    ;

	expr1                                           {-> expr}
	    = {and}   [e1]:expr1 T.kw_and [e2]:expr2	{-> New expr.and(e1.expr, T.kw_and, e2.expr)}
	    | {other} expr2                             {-> expr2.expr}
	    ;

	expr2                                             {-> expr}
	    = {lte}   [e1]:expr3 T.op_lt_eq [e2]:expr3    {-> New expr.lte(e1.expr, T.op_lt_eq, e2.expr)}
	    | {gte}   [e1]:expr3 T.op_gt_eq [e2]:expr3    {-> New expr.gte(e1.expr, T.op_gt_eq, e2.expr)}
        | {lt}    [e1]:expr3 T.op_lt    [e2]:expr3    {-> New expr.lt(e1.expr, T.op_lt, e2.expr)}
        | {gt}    [e1]:expr3 T.op_gt    [e2]:expr3    {-> New expr.gt(e1.expr, T.op_gt, e2.expr)}
        | {eq}    [e1]:expr3 T.op_eq    [e2]:expr3    {-> New expr.eq(e1.expr, T.op_eq, e2.expr)}
        | {other} expr3                               {-> expr3.expr}
        ;

	expr3                                               {-> expr}
	    = {concat} [e1]:expr3 T.op_concat [e2]:expr4    {-> New expr.concat(e1.expr, T.op_concat, e2.expr)}
	    | {other}  expr4                                {-> expr4.expr}
	    ;

	expr4                                             {-> expr}
	    = {add}   [e1]:expr4 T.op_plus  [e2]:expr5    {-> New expr.add(e1.expr, T.op_plus, e2.expr)}
        | {sub}   [e1]:expr4 T.op_minus [e2]:expr5    {-> New expr.sub(e1.expr, T.op_minus, e2.expr)}
        | {other} expr5                               {-> expr5.expr}
        ;

    expr5                                            {-> expr}
        = {mult}  [e1]:expr5 T.op_mult [e2]:expr8    {-> New expr.mult(e1.expr, T.op_mult, e2.expr)}
        | {div}   [e1]:expr5 T.op_div [e2]:expr8     {-> New expr.div(e1.expr, T.op_div, e2.expr)}
        | {other} expr8                              {-> expr8.expr}
        ;
          
    expr8                             {-> expr}
        = {pos}   T.op_plus expr9     {-> New expr.pos(T.op_plus, expr9.expr)}
        | {neg}   T.op_minus expr9    {-> New expr.neg(T.op_minus, expr9.expr)}
        | {not}   T.kw_not expr9      {-> New expr.not(T.kw_not, expr9.expr)}
        | {other} expr9               {-> expr9.expr}
        ;

	expr9 {-> expr}
	    = {id}    T.id                                  {-> New expr.id(T.id)}
        | {str}   T.str_lit                             {-> New expr.str(T.str_lit)}
        | {int}   T.int_lit                             {-> New expr.int(T.int_lit)}
        | {true}  T.kw_true                             {-> New expr.true(T.kw_true)}
        | {false} T.kw_false                            {-> New expr.false(T.kw_false)}
        | {null}  T.kw_null                             {-> New expr.null(T.kw_null)}
        | {me}    T.kw_me                               {-> New expr.me(T.kw_me)}
        | {new}   T.kw_new type                         {-> New expr.new(type)}
        | {par}   T.misc_l_paren expr T.misc_r_paren    {-> expr.expr}
        | {array} T.id arr_expr_list                    {-> New expr.array(T.id, [arr_expr_list.expr])}
        | {call}  call                                  {-> New expr.call(call.call)}
        ;

	arr_expr_list                                                   {-> expr*}
	    = T.misc_l_brack expr T.misc_r_brack arr_expr_list_tail*    {-> [expr, arr_expr_list_tail.expr]}
	    ;
	arr_expr_list_tail                          {-> expr}
	    = T.misc_l_brack expr T.misc_r_brack    {-> expr}
	    ;

	expr_list                     {-> expr*}
	    = expr expr_list_tail*    {-> [expr, expr_list_tail.expr]}
	    ;
	expr_list_tail             {-> expr}
	    = T.misc_comma expr    {-> expr}
	    ;
	
	call                                                             {-> call}
	    = obj_expr? T.id T.misc_l_paren expr_list? T.misc_r_paren    {-> New call(obj_expr.expr, T.id, [expr_list.expr])}
	    ;

	obj_expr                     {-> expr}
	    = expr9 T.misc_period    {-> expr9.expr}
	    ;

Abstract Syntax Tree
	start = [klasses]:klass*
	      ;

	klass = klass_header klass_body klass_footer;

	klass_header = [id]:T.id klass_inherits?;
	klass_inherits = [id]:T.id;
	klass_body = [vars]:var* [methods]:method*;
	klass_footer =  [id]:T.id;

	var = [id]:T.id [tp]:type? [expr]:expr?
	    ;

	method_sig = [id]:T.id [args]:arg* [ret]:type?
	           ;

	method = [sig]:method_sig [vars]:var* [stmts]:stmt*
	       ;

	arg = [id]:T.id [tp]:type
	    ;

	stmt = {assign} [id]:T.id [index]:expr* [op]:T.op_assign [expr]:expr
	     | {if}     [expr]:expr [then]:T.kw_then [if_stmts]:stmt* [else_stmts]:stmt*
	     | {loop}   [while]:T.kw_while [expr]:expr [loop_stmts]:stmt*
	     | {call}   [call]:call
	     ;

	type = {int}      [tp]:T.kw_int
	     | {string}   [tp]:T.kw_string
	     | {bool}     [tp]:T.kw_boolean
	     | {udt}      [tp]:T.id
	     | {array}    [tp]:type [l_brack]:T.misc_l_brack [expr]:expr? [r_brack]:T.misc_r_brack
	     ;

	expr = {or}      [left]:expr [op]:T.kw_or     [right]:expr
	     | {and}     [left]:expr [op]:T.kw_and    [right]:expr
	     | {lte}     [left]:expr [op]:T.op_lt_eq  [right]:expr
	     | {gte}     [left]:expr [op]:T.op_gt_eq  [right]:expr
	     | {lt}      [left]:expr [op]:T.op_lt     [right]:expr
	     | {gt}      [left]:expr [op]:T.op_gt     [right]:expr
	     | {eq}      [left]:expr [op]:T.op_eq     [right]:expr
	     | {concat}  [left]:expr [op]:T.op_concat [right]:expr
	     | {add}     [left]:expr [op]:T.op_plus   [right]:expr
	     | {sub}     [left]:expr [op]:T.op_minus  [right]:expr
	     | {mult}    [left]:expr [op]:T.op_mult   [right]:expr
	     | {div}     [left]:expr [op]:T.op_div    [right]:expr
	     | {pos}     [op]:T.op_plus  [expr]:expr
	     | {neg}     [op]:T.op_minus [expr]:expr
	     | {not}     [op]:T.kw_not   [expr]:expr
	     | {id}      [id]:T.id
	     | {str}     [value]:T.str_lit
	     | {int}     [value]:T.int_lit
	     | {true}    [value]:T.kw_true
	     | {false}   [value]:T.kw_false
	     | {null}    [value]:T.kw_null
	     | {me}      [value]:T.kw_me
	     | {new}     [tp]:type
	     | {array}   [id]:T.id [index]:expr*
	     | {call}    [call]:call
	     ;

	call = [expr]:expr? [id]:T.id [args]:expr*
	     ;
